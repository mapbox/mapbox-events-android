/*
 * Copyright 2013 Chris Banes
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'maven'
apply plugin: 'signing'
def core = project(':libcore')

def isReleaseBuild() {
    return VERSION_NAME.contains("SNAPSHOT") == false
}

def isLocalBuild() {
    if (System.getenv('IS_LOCAL_DEVELOPMENT') != null) {
        return System.getenv('IS_LOCAL_DEVELOPMENT').toBoolean()
    }
    return true
}

def getReleaseRepositoryUrl() {
    return hasProperty('RELEASE_REPOSITORY_URL') ? RELEASE_REPOSITORY_URL
            : "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
}

def getSnapshotRepositoryUrl() {
    return hasProperty('SNAPSHOT_REPOSITORY_URL') ? SNAPSHOT_REPOSITORY_URL
            : "https://oss.sonatype.org/content/repositories/snapshots/"
}

def obtainMavenLocalUrl() {
    return getRepositories().mavenLocal().getUrl()
}

def getRepositoryUsername() {
    return hasProperty('NEXUS_USERNAME') ? NEXUS_USERNAME : ""
}

def getRepositoryPassword() {
    return hasProperty('NEXUS_PASSWORD') ? NEXUS_PASSWORD : ""
}

def isLiteRelease() {
    if (System.getenv('IS_LITE_RELEASE') != null) {
        return System.getenv('IS_LITE_RELEASE').toBoolean()
    }
    return false
}

def isTelem() {
    return project.toString() == "project ':libtelemetry'"
}

afterEvaluate { project ->
    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                if (isLiteRelease()) {
                    VERSION_NAME = VERSION_NAME + "-LITE"
                }

                pom.groupId = GROUP
                pom.artifactId = POM_ARTIFACT_ID
                pom.version = VERSION_NAME

                if (isLocalBuild()) {
                    repository(url: obtainMavenLocalUrl())
                } else {
                    repository(url: getReleaseRepositoryUrl()) {
                        authentication(userName: getRepositoryUsername(), password: getRepositoryPassword())
                    }
                    snapshotRepository(url: getSnapshotRepositoryUrl()) {
                        authentication(userName: getRepositoryUsername(), password: getRepositoryPassword())
                    }
                }

                pom.project {
                    name POM_NAME
                    packaging POM_PACKAGING
                    description POM_DESCRIPTION
                    url POM_URL

                    scm {
                        url POM_SCM_URL
                        connection POM_SCM_CONNECTION
                        developerConnection POM_SCM_DEV_CONNECTION
                    }

                    licenses {
                        license {
                            name POM_LICENCE_NAME
                            url POM_LICENCE_URL
                            distribution POM_LICENCE_DIST
                        }
                    }

                    developers {
                        developer {
                            id POM_DEVELOPER_ID
                            name POM_DEVELOPER_NAME
                        }
                    }

                    if (POM_DESCRIPTION == "Mapbox Android Telemetry Library") {
                        String okhttpString = dependenciesList.okhttp3
                        def okhttp = okhttpString.split(':')

                        String gsonString = dependenciesList.gson
                        def gson = gsonString.split(':')

                        String annotationString = dependenciesList.supportAnnotation
                        def supportAnnotation = annotationString.split(':')

                        def dependenciesArray = [okhttp, gson, supportAnnotation]
                        def exclusionsArray = []

                        if (!VERSION_NAME.contains('LITE')) {
                            String coreString = "com.mapbox.mapboxsdk:mapbox-android-core:" + core.version
                            def coreArray = coreString.split(':')
                            dependenciesArray.add(coreArray)

                            String lifecycleString = dependenciesList.archLifecycleExtensions
                            def lifecycleArray = lifecycleString.split(':')
                            dependenciesArray.add(lifecycleArray)

                            def viewModel = ["viewmodel", "android.arch.lifecycle"]
                            def runtime = ["runtime", "android.arch.core"]
                            def liveData = ["livedata", "android.arch.lifecycle"]
                            exclusionsArray.add(viewModel)
                            exclusionsArray.add(runtime)
                            exclusionsArray.add(liveData)
                        }

                        dependencies {
                            for (String[] thisDependency: dependenciesArray ) {
                                dependency {
                                    groupId thisDependency[0]
                                    artifactId thisDependency[1]
                                    version thisDependency[2]
                                    scope "compile"

                                    if (thisDependency[1] == "extensions") {
                                        exclusions {
                                            for (String[] thisExclusion: exclusionsArray) {
                                                exclusion {
                                                    artifactId thisDependency[1]
                                                    groupId thisDependency[0]
                                                }
                                            }
                                        }
                                    }

                                }
                            }
                        }
                    }
                }
            }
        }
    }

    signing {
        required { isReleaseBuild() && gradle.taskGraph.hasTask("uploadArchives") }
        sign configurations.archives
    }

    task androidJavadocs(type: Javadoc) {
        source = android.sourceSets.main.java.srcDirs
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    }

    task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
        classifier = 'javadoc'
        from androidJavadocs.destinationDir
    }

    task androidSourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.sourceFiles
    }


    artifacts {
        if (isTelem()) {
            if (new File('libtelemetry/build/outputs/aar').exists()) {
                def testNum = 0
                new File('libtelemetry/build/outputs/aar').eachFile() { file ->
                    if (file.toString().contains("lite") && isLiteRelease()) {
                        archives file: file, classifier:testNum
                        testNum++
                    } else if (file.toString().contains("full")) {
                        archives file: file
                    }
                }
            }
        }
        archives androidSourcesJar
        archives androidJavadocsJar
    }


}

// See: https://github.com/chrisbanes/gradle-mvn-push/issues/43#issuecomment-84140513
afterEvaluate { project ->
    android.libraryVariants.all { variant ->
        tasks.androidJavadocs.doFirst {
            classpath += files(variant.javaCompile.classpath.files)
        }
    }
}
