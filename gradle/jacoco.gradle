apply plugin: 'jacoco'

project.afterEvaluate {
    // Grab all build types and product flavors
    def buildTypes = android.buildTypes.collect { type -> type.name }
    def productFlavors = android.productFlavors.collect { flavor -> flavor.name }

    // When no product flavors defined, use empty
    if (!productFlavors) productFlavors.add('')

    productFlavors.each { productFlavorName ->
        buildTypes.each { buildTypeName ->
            def sourceName, sourcePath

            sourceName = sourcePath = productFlavorName ?
                    "${productFlavorName}${buildTypeName.capitalize()}" : "${buildTypeName}"

            def testTaskName = "test${sourceName.capitalize()}UnitTest"
            def coverageTaskName = "${testTaskName}Coverage"

            // Create coverage task of form 'testFlavorTypeUnitTestCoverage' depending on 'testFlavorTypeUnitTest'
            task "${coverageTaskName}"(type: JacocoReport, dependsOn: "$testTaskName") {
                group = 'Reporting'
                description = "Generate Jacoco coverage reports for the ${sourceName.capitalize()} build."

                classDirectories = fileTree(
                        dir: "${project.buildDir}/intermediates/javac/${sourcePath}",
                        excludes: ['**/R.class',
                                   '**/R$*.class',
                                   '**/BuildConfig.*',
                                   '**/Manifest*.*',
                                   '**/*$Lambda$*.*'])

                sourceDirectories = files(["src/main/java",
                                           "src/$productFlavorName/java",
                                           "src/$buildTypeName/java"])
                println("${project.buildDir}")

                executionData = fileTree(dir: "${project.buildDir}",
                        includes: ["jacoco/${testTaskName}.exec",
                                   "jacoco/*.ec",
                                   "outputs/code_coverage/${sourceName}AndroidTest/connected/*.ec"])

                reports {
                    xml.enabled = true
                    html.enabled = true
                }
            }
            build.dependsOn "${coverageTaskName}"
        }
    }
}
