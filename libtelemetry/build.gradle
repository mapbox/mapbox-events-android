apply plugin: 'com.android.library'

// Re-apply runtime required subproject gradle scripts and classpaths,
// so that Maps SDK doesn't rely on them being served from the top level build.gradle
apply from: "../gradle/dependencies.gradle"

version = VERSION_NAME

android {
    compileSdkVersion androidVersions.compileSdkVersion

    defaultConfig {
        minSdkVersion androidVersions.minSdkVersion
        targetSdkVersion androidVersions.targetSdkVersion
        versionCode 1
        versionName version
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'proguard-consumer.pro'
    }

    buildTypes {
        debug {
            testCoverageEnabled true
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    flavorDimensions "version"
    productFlavors {
        full {
            dimension "version"
        }

        lite {
            dimension "version"
            versionNameSuffix "-lite"
        }
    }
}

dependencies {
    implementation dependenciesList.okhttp3
    implementation dependenciesList.gson
    implementation dependenciesList.supportAnnotation
    implementation dependenciesList.workManager
    implementation project(':libcore')

    fullImplementation dependenciesList.supportCoreUtils

    testImplementation dependenciesList.junit
    testImplementation dependenciesList.mockito
    testImplementation dependenciesList.okhttp3Mockwebserver
    testImplementation dependenciesList.okhttpTls
    testImplementation dependenciesList.commonsIO
    testImplementation dependenciesList.assertJ

    androidTestImplementation dependenciesList.mockitoAndroid
    androidTestImplementation dependenciesList.testRunner
    androidTestImplementation dependenciesList.testRules
    androidTestImplementation dependenciesList.workManagerTesting
}